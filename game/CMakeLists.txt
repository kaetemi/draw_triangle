
FILE(GLOB SRCS *.cpp)
FILE(GLOB HDRS *.h)
FILE(GLOB INLS *.inl)
FILE(GLOB RSRC *.rc *.manifest)

FILE(GLOB_RECURSE GSRC
  "shaders/*"
)

SOURCE_GROUP("" FILES ${SRCS} ${HDRS} ${INLS} ${RSRC})
SOURCE_GROUP("shaders" FILES ${GSRC})

FOREACH(GLSL ${GSRC})
  GET_FILENAME_COMPONENT(FILE_NAME ${GLSL} NAME)
  GET_FILENAME_COMPONENT(FILE_EXT ${GLSL} LAST_EXT)
  STRING(SUBSTRING ${FILE_EXT} 1 -1 FILE_EXT)
  SET(SPIRV_OUTPUT "${PROJECT_BINARY_DIR}/game/shaders/${FILE_NAME}.spv")
  IF(FILE_EXT MATCHES "_6")
    ADD_CUSTOM_COMMAND(
      OUTPUT ${SPIRV_OUTPUT}
      COMMAND ${GAME_DXC} -T ${FILE_EXT} -spirv -Fo ${SPIRV_OUTPUT} ${GLSL}
      DEPENDS ${GLSL})
  ELSE()
    ADD_CUSTOM_COMMAND(
      OUTPUT ${SPIRV_OUTPUT}
      COMMAND ${GAME_GLSL_VALIDATOR} -V ${GLSL} -o ${SPIRV_OUTPUT}
      DEPENDS ${GLSL})
  ENDIF()
  LIST(APPEND SPIRV_BINARY_FILES ${SPIRV_OUTPUT})
  SET(GLSL_OUTPUT "${PROJECT_BINARY_DIR}/game/shaders/${FILE_NAME}.glsl")
  ADD_CUSTOM_COMMAND(
    OUTPUT ${GLSL_OUTPUT}
    COMMAND ${GAME_SPIRV_CROSS} ${SPIRV_OUTPUT} --output ${GLSL_OUTPUT} --version 440
    DEPENDS ${SPIRV_OUTPUT})
  LIST(APPEND SPIRV_BINARY_FILES ${GLSL_OUTPUT})
  SET(SPIRV_OUTPUT_INL "${SPIRV_OUTPUT}.inl")
  ADD_CUSTOM_COMMAND(
    OUTPUT ${SPIRV_OUTPUT_INL}
    COMMAND ${PYTHON_EXECUTABLE} ${CMAKE_CURRENT_SOURCE_DIR}/../scripts/bin2c.py ${SPIRV_OUTPUT} ${SPIRV_OUTPUT_INL}
    DEPENDS ${SPIRV_OUTPUT})
  LIST(APPEND SPIRV_BINARY_FILES ${SPIRV_OUTPUT_INL})
  SET(GLSL_OUTPUT_INL "${GLSL_OUTPUT}.inl")
  ADD_CUSTOM_COMMAND(
    OUTPUT ${GLSL_OUTPUT_INL}
    COMMAND ${PYTHON_EXECUTABLE} ${CMAKE_CURRENT_SOURCE_DIR}/../scripts/str2c.py ${GLSL_OUTPUT} ${GLSL_OUTPUT_INL}
    DEPENDS ${GLSL_OUTPUT})
  LIST(APPEND SPIRV_BINARY_FILES ${GLSL_OUTPUT_INL})
  SET(SHADER_INL "${PROJECT_BINARY_DIR}/game/shaders/${FILE_NAME}.inl")
  ADD_CUSTOM_COMMAND(
    OUTPUT ${SHADER_INL}
    COMMAND ${PYTHON_EXECUTABLE} ${CMAKE_CURRENT_SOURCE_DIR}/../scripts/shader_inl.py ${FILE_NAME} ${SHADER_INL}
    DEPENDS ${GLSL})
  LIST(APPEND SPIRV_BINARY_FILES ${SHADER_INL})
ENDFOREACH()

ADD_CUSTOM_TARGET(
  shaders
  DEPENDS ${SPIRV_BINARY_FILES}
)

ADD_CUSTOM_COMMAND(TARGET shaders PRE_BUILD
  COMMAND ${CMAKE_COMMAND} -E make_directory "${PROJECT_BINARY_DIR}/game/shaders/"
)

INCLUDE_DIRECTORIES(
  ${PROJECT_BINARY_DIR}/game/shaders
)

SET_PROPERTY(SOURCE ${CMAKE_CURRENT_BINARY_DIR}/../dependencies/gl3w/src/gl3w.c PROPERTY GENERATED 1)

ADD_EXECUTABLE(game WIN32
  ${SRCS}
  ${HDRS}
  ${INLS}
  ${RSRC}
  ${GSRC}
)

ADD_DEPENDENCIES(game
  shaders
  gl3w
)

TARGET_LINK_LIBRARIES(game PUBLIC
  gl3w
  fmt
)
